## An example of kinematics diagram for a 3-DOF robot arm
## 
## Author : Alex Dumitrache <alex@cimr.pub.ro>
## License: GPL

def blue<>
input{lib/defaults.sk}
input{lib/objects.sk}

def J1 180
def J2 180
def J3 0
def J4 90

def arrowarc 
{
    sweep [style=thick]{215, rotate(1, [Z])} (1,0)
    def p1 (1,0) then rotate(210, [Z])
    def p2 (1,0) then rotate(210+30, [Z])
    line [style=thick, style=->] (p1)(p2)
}

% The main drawing
put {scale(0.5)}
{   
    # base
    put {scale(1)}
    {
        put{scale([8,8,0.01]) * translate([0,0,-0.5])}{box}
        put{scale(1.5)}
        {
            {coordsys}
            special |\path #1 node[left] {$Z_0$}
                           #2 node[below] {$X_0$}
                           #3 node[right] {$Y_0$};|(z)(x)(y)(o)
        }       
    
        line [style=dashed] (0,0,0)(10,0,0)

        # link1
        put {translate([10,0,0]) * rotate(J1, [Z])}
        {
            put{scale([1,1,3])}{cylinder}

            put{scale([3,4,4])* rotate(180, [Z])}
            {
                {coordsys}
                special |\path #1 node[right] {$Z_1$}
                                #2 node[above] {$X_1$}
                                #3 node[above] {$Y_1$};|(z)(x)(y)(o)
            }
            
            put {translate([0,0,5.5]) * rotate(30, [Z])}{arrowarc}
            special|\node at #1 {$\theta_1$};|(0,0,6)

            line [style=dashed] (0,0,0)(-6,0,0)
        
            # link2
            put {translate([-6,0,0]) * rotate(-90, [X]) * rotate(J2+90, [Z])}
            {
                put{scale([1,1,3])}{cylinder}
                put{translate([0,0,0]) * rotate(-90, [Z])}
                {
                    put {scale([3,4,4])}
                    {
                        {coordsys}
                        special |\path #1 node[left] {$Z_2$}
                                       #2 node[below] {$X_2$}
                                       #3 node[left] {$Y_2$};|(z)(x)(y)(o)
                    }
                                   
                    put {translate([0,0,-4.5]) * rotate(120, [Z])}{arrowarc}
                    special|\node at #1 {$\theta_2$};|(0,0,-4.5)
                }

                line [style=dashed] (0,0,0)(5,-8,0)

                # link3
                put {translate([5,-8,0]) * rotate(J3, [Z]) * rotate(-90, [Z])}
                {
                    put{scale([1,1,3])}{cylinder}
                    put{scale([3,4,4])}
                    {
                        {coordsys}
                        special |\path #1 node[below] {$Z_3$}
                                       #2 node[below] {$X_3$}
                                       #3 node[left] {$Y_3$};|(z)(x)(y)(o)
                    }

                    put {translate([0,0,-4.5]) * rotate(120, [Z])}{arrowarc}
                    special|\node at #1 {$\theta_3$};|(0,0,-4.5)

                    line [style=dashed] (0,0,0)(0,-12,0)
                    
                    # end effector

                    put {rotate(J4, [Z]) * translate([-12,0,0])}
                    {
                        put{scale([1,1,1])}{box}

                    }
                }
            }
        }
    }
}



global 
{ 
    language tikz 
    camera rotate(0, (1,0,0)) * view((1.5,2,1),(0,0,0),[0,0,1]) * rotate(110, (0,0,1))
}


